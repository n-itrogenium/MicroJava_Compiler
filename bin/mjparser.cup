  
package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;
import rs.ac.bg.etf.pp1.test.Compiler;
import rs.ac.bg.etf.pp1.test.CompilerError;


parser code {:
	
	boolean errorDetected = false;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
        CompilerExt.errors.add(new CompilerError(((Symbol)info).left, msg.toString(), CompilerError.CompilerErrorType.SYNTAX_ERROR));
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROGRAM, BREAK, ENUM, DO, WHILE, NEW, CONST, CLASS, RECORD, EXTENDS, READ, GOTO;
terminal SEMI, RETURN, COMMA, PRINT, VOID, CONTINUE,  SWITCH, CASE;
terminal LBRACE, RBRACE, LBRACKET, RBRACKET, LPARENT, RPARENT, COLON, QUESTION;
terminal EQUAL, NOTEQ, GREATEREQ, GREATER, LESSEQ, LESS, PLUS, MINUS, MUL, DIV, MOD;
terminal AND, OR, ASSIGN, INC, DEC, DOT, YIELD, SWAP;
terminal Integer NUMBER;
terminal Character CHAR;
terminal Integer BOOL;
terminal String IDENT;
terminal IF, ELSE;

nonterminal Program, DeclList, Declaration, VarDecl, VarDeclList, VarDeclSingle, ArrayOptional, ConstDecl, RecordDecl, RecVarDecl;
nonterminal ConstDeclList, ConstDeclSingle, MethodDeclList, MethodDecl, MethodTypeName, RetType, Statement, SingleStatement, FormPars, FormalParSingle;
nonterminal StatementList, DesignatorStatement, ArrayElement, ExpressionList, ExprSingle, ActParsOpt, IfStart, IfEnd, ThenStatement, ElseOpt, OrOp, RelOp;
nonterminal PrintParamOpt, AddOp, MulOp, VarDeclOnly;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Designator, CondFact, MethodName, Label;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, ReturnOpt, Term, Condition, CondTerm, Expression;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factor, ConstInit;

precedence left ELSE;

Program ::= (Program) PROGRAM ProgName:p DeclList LBRACE MethodDeclList RBRACE
;

ProgName ::= (ProgName) IDENT:pName 
;

DeclList 		::= (DeclarationList) DeclList Declaration
				|	(NoDeclaration)
;

Declaration 	::= (RecordDeclaration) RecordDecl
				|	(VarDeclaration) VarDecl
				|	(ConstDeclaration) ConstDecl
;

RecordDecl		::= RECORD IDENT:recordName LBRACE RecVarDecl RBRACE;

RecVarDecl		::= (VarDeclarations) RecVarDecl VarDecl
				|	(NoVarDecl)
; 

VarDecl 		::= (VarDeclExpr) Type VarDeclList SEMI
				|	(ErrorVarDecl) error
;

Type ::= (Type) IDENT:typeName 
;


VarDeclList 	::= (VarDeclarationList) 	VarDeclList COMMA VarDeclSingle
				| 	(VarDeclarationSingle) 	VarDeclSingle
				|	(ErrorVarDeclList) error
;

VarDeclSingle 	::= (VarDeclSingle) IDENT:varName ArrayOptional
;				

ArrayOptional	::= (Array) LBRACKET RBRACKET
				|	(NotArray) 
;

ConstDecl	::= (ConstDecl) CONST Type ConstDeclList SEMI
;

ConstDeclList	::= (ConstDeclarationList) ConstDeclList COMMA ConstDeclSingle
				| 	(ConstDeclarationSingle) ConstDeclSingle
;

ConstDeclSingle ::= (ConstDeclSingle) IDENT:constName ASSIGN ConstInit
;

ConstInit		::= (BooleanVal) BOOL:boolValue
				| (NumberVal)	NUMBER:numValue
				|	(CharVal) CHAR:charValue
;

MethodDeclList 	::= (MethodDeclarations) MethodDeclList MethodDecl
				| 	(NoMethodDecl)
;

MethodDecl 		::= (MethodDecl) RetType MethodName LPARENT FormPars RPARENT VarDeclOnly LBRACE StatementList RBRACE 
;
			   
MethodName 		::= (MethodName) IDENT:methodName;

FormPars		::= (FormalParameters) FormPars COMMA FormalParSingle
				|	(FormParamSingle) FormalParSingle
;

FormalParSingle	::= (FormalParam) Type IDENT:formParamName ArrayOptional
				|	(NoFormalParam) ;

VarDeclOnly		::=	(VarDeclOnlyExpr) VarDeclOnly VarDecl
			  	|	(NoVars) /*epsilon*/; 


RetType			::= (ReturnType) Type
				| 	(ReturnVoid) VOID;
				
Statement		::= (LabeledStmt) Label COLON SingleStatement
				| 	(SingleStmt) SingleStatement
				| 	(ListStatement) LBRACE StatementList RBRACE;

Label			::= (Label) IDENT:labelName;
				
SingleStatement		::= (DesignatorStatementExpr) DesignatorStatement SEMI
				|	(IfStatement)		IF IfStart Condition IfEnd ThenStatement ElseOpt
				|	(DoWhileStatement)	DO Statement WHILE LPARENT Condition RPARENT SEMI
				|	(BreakStatement)	BREAK SEMI
				|	(ContinueStatement)	CONTINUE SEMI
				|	(ReturnStatement)	RETURN ReturnOpt SEMI
				|	(ReadStatement)		READ LPARENT Designator RPARENT SEMI
				|	(PrintStatement)	PRINT LPARENT Expression PrintParamOpt RPARENT SEMI
				|	(GotoStatement)		GOTO IDENT:labelName SEMI
;

StatementList	::= (StmtListExpr) StatementList Statement
				| 	(NoStmt) ;
		
DesignatorStatement ::= (AssignExpr) Designator ASSIGN Expression
					|	(IncExpr)	Designator INC
					|	(DecExpr)	Designator DEC
					|	(CallExpr)  Designator LPARENT ExpressionList RPARENT
;

Designator		::= (DesList) Designator DOT IDENT:designatorName ArrayElement
				|	(DesSingle) IDENT:designatorName ArrayElement; 

ArrayElement	::= (ArrayElem)	LBRACKET Expression RBRACKET
				|	(NotArrayElem)
;

ExpressionList	::= (ExprList) ExpressionList COMMA ExprSingle
				| (ExpressionSingle) ExprSingle;

ExprSingle		::= (Expr) Expression
				| (NoExpr) ;
				
IfStart			::= (IfStartStmt) LPARENT;

IfEnd			::= (IfEndStmt) RPARENT;

ThenStatement 	::= (ThenStatement) Statement;

ElseOpt			::= (ElseStatement) ELSE Statement
				|	(NoElseStatement);

Condition		::= (CondOr) 			Condition OrOp CondTerm
				| 	(CondTermSingle)	CondTerm;
				
OrOp			::= (OrOp) OR;

CondTerm		::= (CondAnd) 			CondTerm AND CondFact
				|	(CondFactSingle)	CondFact;

CondFact		::= (CondFactRelOp)		Expression RelOp Expression
				|	(CondFactNoRelOp) 	Expression;

RelOp			::= (EqualRelOp)		EQUAL 
				|	(NotEqualRelOp)		NOTEQ 
				| 	(GreaterEqRelOp)	GREATEREQ 
				|	(GreaterRelOp)		GREATER 
				| 	(LessEqRelOp)		LESSEQ 
				| 	(LessRelOp)			LESS;

Expression			::= (NegativeTerm)	MINUS Term
				|	(PositiveTerm)	Term
				|	(NotFirstTerm)	Expression AddOp Term;

Term			::= (TermExpr)		Term MulOp Factor
				|	(TermFactor)	Factor
				;
				
ReturnOpt	::= (ReturnExpr) Expression
				|	(NoExpression)
				;

Factor			::= (DesFactor) 		Designator ActParsOpt
				|	(ConstFactor) 		ConstInit
				|	(NewArrayFactor)	NEW Type LBRACKET Expression RBRACKET
				|	(ExprFactor)		LPARENT Expression RPARENT;

ActParsOpt		::= (ActPars) LPARENT ExpressionList RPARENT
				| (NoActPars) ;


PrintParamOpt	::= (PrintParam)	COMMA NUMBER:printWidth
				|	(NoPrintParam) /* epsilon */;

AddOp			::= (Plus)  PLUS
				|	(Minus) MINUS;

MulOp			::= (MulOperator) MUL
				|	(DivOperator) DIV
				|	(ModOperator) MOD;


